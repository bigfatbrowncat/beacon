import("//skia/features.gni")

# Application base project

if (is_win) {
  # Set this to false if you want to support old Windows (that lacks Windows Store)
  using_win_app_api = true
}

static_library("app_base") {
  configs -= [ "//build/config/clang:find_bad_constructs" ]
  include_dirs = [ "." ]
  defines = []
  configs += [
    "//skia:skia_config",
    "//skia:skia_library_config",
    "//build/config/compiler:no_chromium_code",
  ]
  public_configs = [ "//skia:skia_config" ]

  public_deps = [
    "//skia:gpu_tool_utils",
    "//skia:skia",
  ]
  sources = [
    "app_base/GLWindowContext.cpp",
    "app_base/GLWindowContext.h",
    "app_base/Window.cpp",
    "app_base/Window.h",
    "app_base/Application.cpp",
    "app_base/Application.h",
  ]
  libs = []

  if (skia_use_dawn) {
    sources += [ "app_base/DawnWindowContext.cpp" ]
  }

  if (is_android) {
    sources += [
      "app_base/android/GLWindowContext_android.cpp",
      "app_base/android/RasterWindowContext_android.cpp",
      "app_base/android/Window_android.cpp",
      "app_base/android/main_android.cpp",
      "app_base/android/surface_glue_android.cpp",
    ]
    libs += [ "android" ]
  } else if (is_linux) {
    sources += [
      "app_base/unix/GLWindowContext_unix.cpp",
      "app_base/unix/RasterWindowContext_unix.cpp",
      "app_base/unix/Window_unix.cpp",
      "app_base/unix/Window_unix.h",
      "app_base/unix/keysym2ucs.c",
      "app_base/unix/main_unix.cpp",
    ]
    if (skia_use_dawn) {
      if (dawn_enable_vulkan) {
        sources += [ "app_base/unix/DawnVulkanWindowContext_unix.cpp" ]
        defines += [ "VK_USE_PLATFORM_XCB_KHR" ]
        libs += [ "X11-xcb" ]
      }
    }
    libs += [
      "GL",
      "X11",
    ]
  } else if (is_win) {
    if (using_win_app_api) {
      defines += [ "USING_WINDOWS_APP_API" ]
      libs += [ "WindowsApp.lib" ]
    }
    sources += [
      "app_base/win/GLWindowContext_win.cpp",
      "app_base/win/RasterWindowContext_win.cpp",
      "app_base/win/Window_win.cpp",
      "app_base/win/Window_win.h",
      "app_base/win/main_win.cpp",
    ]
    if (skia_use_angle) {
      sources += [ "app_base/win/ANGLEWindowContext_win.cpp" ]
    }
    if (skia_use_dawn) {
      if (dawn_enable_d3d12) {
        sources += [ "app_base/win/DawnD3D12WindowContext_win.cpp" ]
      }
    }
  } else if (is_mac) {
    sources += [
      "app_base/mac/GLWindowContext_mac.mm",
      "app_base/mac/RasterWindowContext_mac.mm",
      "app_base/mac/Window_mac.mm",
      "app_base/mac/Window_mac.h",
      "app_base/mac/main_mac.mm",
    ]
    if (skia_use_dawn) {
      if (dawn_enable_metal) {
        sources += [ "app_base/mac/DawnMTLWindowContext_mac.mm" ]
      }
    }
    libs += [
      "QuartzCore.framework",
      "Cocoa.framework",
      "Foundation.framework",
    ]
  } else if (is_ios) {
    sources += [
      "app_base/ios/GLWindowContext_ios.mm",
      "app_base/ios/RasterWindowContext_ios.mm",
      "app_base/ios/Window_ios.mm",
      "app_base/ios/main_ios.mm",
    ]
    libs += [ "QuartzCore.framework" ]
  }

  if (skia_use_vulkan) {
    sources += [ "app_base/VulkanWindowContext.cpp" ]
    if (is_android) {
      sources += [ "app_base/android/VulkanWindowContext_android.cpp" ]
    } else if (is_linux) {
      sources += [ "app_base/unix/VulkanWindowContext_unix.cpp" ]
      libs += [ "X11-xcb" ]
    } else if (is_win) {
      sources += [ "app_base/win/VulkanWindowContext_win.cpp" ]
    }
  }

  if (skia_use_metal) {
    sources += [ "app_base/MetalWindowContext.mm" ]
    if (is_mac) {
      sources += [ "app_base/mac/MetalWindowContext_mac.mm" ]
    } else if (is_ios) {
      sources += [ "app_base/ios/MetalWindowContext_ios.mm" ]
    }
  }

  deps = [
    "//skia:tool_utils",
  ]
  if (is_android) {
    deps += [ "//third_party/native_app_glue" ]
  }
  if (skia_use_angle) {
    deps += [ "//third_party/angle2" ]
  }

  if (is_win) {
    sources += [ 
      "//third_party/skia/src/utils/win/SkWGL_win.cpp",
    ]
  }
  
  sources += [ "//third_party/skia/src/gpu/gl/GrGLUtil.cpp" ]
}

# Resource embedding tasks

action("icudtl_object") {
  deps = [ 
    "//third_party/icu:icudata", 
  ]
  
  icudtl_input = "$root_build_dir/icudtl.dat"
  symbol_name = "icudtl_dat"
  
  inputs = [ "$icudtl_input" ]

  icudtl_output = "$root_build_dir/icudtl.dat.c"
   
  script = "scripts/bin2c.py"
  
  args = [
    "--input",        rebase_path(icudtl_input),
    "--output",       rebase_path(icudtl_output),
    "--symbol_name",  symbol_name,
  ]
  
  outputs = [ "$icudtl_output" ]
}

action("blink_resources_object") {
  deps = [
    "//third_party/blink/public:resources",
  ]

  blinkres_input = "$root_build_dir/gen/third_party/blink/public/resources/blink_resources.pak"
  symbol_name = "blink_resources_pak"

  inputs = [ "$blinkres_input" ]

  script = "scripts/bin2c.py"

  blinkres_output = "$root_build_dir/blink_resources.pak.c"
  
  args = [
    "--input",        rebase_path(blinkres_input),
    "--output",       rebase_path(blinkres_output),
    "--symbol_name",  symbol_name,
  ]
  

  outputs = [ "$blinkres_output" ]
}

# Main project

executable("Beacon") {
  libs = []
  configs -= [ "//build/config/clang:find_bad_constructs" ]
  sources = [

    # SDK sources can depend only on the standard libraries
    # and other Beacon sources
    "src/bn/sdk/BNBackendController.cpp",
    "src/bn/sdk/BNBackendController.h",
    "src/bn/sdk/BNFI.cpp",
    "src/bn/sdk/BNFI.h",

    # Implementation sources can depend on SDK sources 
    # and chromium sources, they implement only SDK interfaces
    "src/bn/impl/BNApp.cpp",
    "src/bn/impl/BNApp.h",
    "src/bn/impl/BNBlinkPlatformImpl.cpp",
    "src/bn/impl/BNBlinkPlatformImpl.h",
    "src/bn/impl/main.cpp",

    # The glue sources include classes necessary to communicate with
    # the chromium sources directly
    "src/bn/glue/my_frame_test_helpers.cc",
    "src/bn/glue/my_frame_test_helpers.h",
    "src/bn/glue/my_blink_platform_impl.cc",
    "src/bn/glue/my_blink_platform_impl.h",
    "src/bn/glue/my_webthemeengine_impl_default.cc",
    "src/bn/glue/my_webthemeengine_impl_default.h",
    "src/bn/glue/my_webthemeengine_impl_conversions.cc",
    "src/bn/glue/my_webthemeengine_impl_conversions.h",
    "src/bn/glue/my_webthemeengine_impl_mac.cc",
    "src/bn/glue/my_webthemeengine_impl_mac.h",
    "src/bn/glue/SkLoadICU.cpp",
    "src/bn/glue/SkLoadICU.h",

    # We are borrowing some resources from the "content" library, 
    # but trying to do that as few as possible
    "../content/renderer/compositor/layer_tree_view.cc",
    "../content/renderer/compositor/layer_tree_view.h",
    "../content/renderer/compositor/layer_tree_view_delegate.h",
  ]
  
  sources += [
    "$root_build_dir/icudtl.dat.c",
    "$root_build_dir/blink_resources.pak.c",
  ]  

  defines = []

  if (is_win) {
    cflags_cc = [ "/std:c++17"]
  } else {
    cflags_cc = [ "-std=c++17"]
  }
  
  if (is_mac) {
    libs += [
      "AppKit.framework",
      "CoreFoundation.framework",
      "CoreGraphics.framework",
      "CoreText.framework",
      "Metal.framework",
      "Foundation.framework",
      "OpenGL.framework",
    ]
  }
  
  include_dirs = [
    ".",
    "src",
    "//third_party/icu/source/common",
    "//third_party/boringssl/src/include",
  ]

  defines += [ "SK_USING_THIRD_PARTY_ICU" ]
  if (is_component_build) {
    defines += [ "COMPONENT_BUILD" ]
  }

  defines += [ "CONTENT_IMPLEMENTATION" ]
#      defines += [ "BLINK_IMPLEMENTATION=1" ]
#      defines += [ "BLINK_PLATFORM_IMPLEMENTATION=1" ]


  deps = [
    # Our internal deps
    ":app_base",

    # Embedding the resources into our binary
    "//third_party/icu:icudata",
    "//third_party/blink/public:resources",
    ":icudtl_object",
    ":blink_resources_object",

    # Skia deps
    "//skia:flags",
    "//skia:gpu_tool_utils",
    "//skia:skia",
    "//skia:tool_utils",
    "//skia:pathkit",

    # Blink
    "//third_party/blink/public:blink",
    "//third_party/blink/public:image_resources",
    "//third_party/blink/public/strings",
    "//third_party/blink/renderer/core:core",
    "//third_party/blink/renderer/platform:platform",
    "//third_party/blink/renderer/modules:modules",
    "//third_party/blink/renderer/bindings:generate_v8_bindings",

    # Other Chromium components
    "//cc:cc",
    "//components/ukm:ukm",
    "//components/discardable_memory/service:service",

    "//ui/events:events",
    "//ui/events/blink:blink",
    "//ui/native_theme:native_theme_browser",

    "//mojo/core/embedder:embedder",

        
        #"//content/public/common:interfaces",
        #"//content/common:mojo_bindings",
        #"//content/child:child",

  ]
  if (is_linux && skia_use_x11) {
    deps += ["//ui/events/devices/x11:x11"]
  }
}
